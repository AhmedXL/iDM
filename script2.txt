on *:TEXT:*:#iDM.Staff: {
  if (# == #iDM || # == #iDM.Staff) && ($me != iDM) { halt }
  if ($nick !isop #) && (!$readini(admins.ini,admins,$address($nick,3))) { halt }
  tokenize 32 $remove($1-,$chr(36),$chr(37))
  if ($1 == !on) {
    if (!$2) { notice $nick $logo(ERROR) To use !on/off, type $1 attack,attack,attack,etc. | halt }
    else enable $2 $nick #
  }
  elseif ($1 == !off) {
    if (!$2) { notice $nick $logo(ERROR) To use !on/off, type $1 attack,attack,attack,etc. | halt }
    else disable $2 $nick #
  }
}
alias enable {
  tokenize 32 $replace($1,$chr(44),$chr(58)) $2-
  if ($1 == all) { 
    remini -n OnOff.ini $3
    notice $2 $logo(ENABLE) All attacks have been turned on.
    halt
  }
  var %a 1
  while ($gettok($1,%a,58)) {
    if ($attack($gettok($1,%a,58))) && ($readini(OnOff.ini,$3,$gettok($1,%a,58))) { var %b %b $gettok($1,%a,58) | remini -n OnOff.ini $3 $gettok($1,%a,58) }
    else { var %c %c $gettok($1,%a,58) } 
    inc %a
  }
  notice $2 $logo(ENABLE) $iif(%b,$s1(Enabled) $+ : $replace(%b,$chr(32),$chr(44))) $iif(%c,$s1(Errors) $+ : $replace(%c,$chr(32),$chr(44)) (These are either already on, or not an attack))
}
alias disable {
  tokenize 32 $replace($1,$chr(44),$chr(58)) $2-
  var %a 1
  while ($gettok($1,%a,58)) {
    if ($attack($gettok($1,%a,58))) && (!$readini(OnOff.ini,$3,$gettok($1,%a,58))) { var %b %b $gettok($1,%a,58) | writeini -n OnOff.ini $3 $gettok($1,%a,58) true }
    else { var %c %c $gettok($1,%a,58) } 
    inc %a
  }
  notice $2 $logo(DISABLE) $iif(%b,$s1(Disabled) $+ : $replace(%b,$chr(32),$chr(44))) $iif(%c,$s1(Errors) $+ : $replace(%c,$chr(32),$chr(44))(These are either already off, or not an attack))
}
alias attack {
  if ($1 == whip) || ($1 == dds) || ($1 == ags) || ($1 == bgs) || ($1 == sgs) || ($1 == zgs) || ($1 == dh) || ($1 == gmaul) || ($1 == guth) || ($1 == surf) || ($1 == dclaws) || ($1 == dmace) || ($1 == dhally) || ($1 == dspear) || ($1 == dscim) || ($1 == dlong) || ($1 == d2h) || ($1 == ssword) || ($1 == anchor) || ($1 == vlong) || ($1 == vpsear) || ($1 == statius) || ($1 == cbow) || ($1 == dbow) || ($1 == mjavelin) || ($1 == ice) || ($1 == blood) {
    return $true
  }   
}
